
*-----------------------------------------------------------
* Title      :
* Written by :
* Date       :
* Description:
*-----------------------------------------------------------
        ORG    $1000
                
START    
        *INICIALIZO LOS REGISTROS*
        
        JSR RESET  

        *BLOQUE PRINCIPAL DEL PROGRAMA*
 
        JSR TXTINICIO ; INICIO DEL PROGRAMA
        JSR NUMMATRICES ; PIDO EL NÚMERO DE MATRICES
        JSR MATRICES ; INTRODUCIR LAS MATRICES        
        JSR OPERACIONES ; REALIZAR OPERACIONES

        SIMHALT

*REGISTROS PARA EL BLOQUE INTRODUCIR MATRICES*

*D7 --> CONTADOR DE MATRICES
*D6 --> CONTADOR DE FILAS
*D5 --> CONTADOR DE COLUMNAS
*D4 --> POSICION DEL VALOR A INTRODUCIR  
*D3 --> VALOR CON DECIMALES
*D0 --> OPERACIONES

*A6 --> DIRECCIONES DEL NÚMERO DE FILAS
*A5 --> DIRECCIONES DEL NÚMERO DE COLUMNAS
*A4 --> DIRECCIONES DE LOS VALORES
*A1 --> TEXTO A IMPRIMIR POR LA CONSOLA
        
TXTINICIO

        *IMPRIMIR MENSAJE DE INICIO*
        
        MOVE.L #INI,A1
        JSR IMPTXT
        
        MOVE.L #INSTRA,A1
        JSR IMPTXT        
        MOVE.L #INSTRB,A1
        JSR IMPTXT        
        MOVE.L #INSTRC,A1
        JSR IMPTXT        
        MOVE.L #INSTRD,A1
        JSR IMPTXT        
        MOVE.L #INSTRE,A1
        JSR IMPTXT
        
        RTS
              
NUMMATRICES  

        *PEDIR NÚMERO DE MATRICES*
        
        MOVE.L #NUM,A1
        JSR IMPTXT ;PIDO EL NÚMERO
        
        JSR PEDIRNUMERO
        MOVE.L INUM,D0
        MOVE.B D0, MNUM ; GUARDO EL NÚMERO DE MATRICES
      
        
        RTS
        
   
MATRICES
        
       *IMPRIMO EL TÍTULO*
                
        JSR NUEVAPAGINA
        
        MOVE.L #MAT,A1
        JSR IMPTXT ; IMPRIMO '*MATRIZ: '
        
        MOVE.L D7,ONUM
        JSR IMPNUM ; IMPRIMO NÚMERO DE LA MATRIZ
        
        JSR ASTERISCO ; IMPRIMO ASTERISCO
        JSR SALTOLINEA ; SALTO DE LINEA
        JSR NUEVALINEA ;SALTO DE LINEA Y PRINCIPIO DE LINEA       
        
        *PIDO NÚMERO DE FILAS Y DE COLUMNAS DE LA MATRIZ*
        
        JSR TAMANO
        
        *PIDO QUE INTRODUZCA LOS VALORES*
        
        MOVE.L #VAL,A1
        JSR IMPTXT  ; IMPRIMO LA INSTRUCCION
        
        JSR IMATRIZ ; PIDO LOS VALORES DE LA MATRIZ
        
        *COMPRUEBO SI QUEDAN MATRICES POR INTRODUCIR*
        CMP.B MNUM,D7
        BNE MATRICES
        
        RTS
               
TAMANO  
        *PEDIR EL NÚMERO DE FILAS DE LA MATRIZ*
        
        MOVE.L #FIL,A1
        JSR IMPTXT ; IMPRIMO LA INSTRUCCION
        
        JSR PEDIRNUMERO
        MOVE.L INUM,D0
        MOVE.B D0,(A6,D7) ; GUARDO EL NÚMERO DE FILAS
        
        JSR NUEVALINEA
        
        *PEDIR EL NÚMERO DE COLUMNAS DE LA MATRIZ*
        
        MOVE.L #COL,A1
        JSR IMPTXT ; IMPRIMO LA INSTRUCCION
        
        JSR PEDIRNUMERO
        MOVE.L INUM,D0
        MOVE.B D0,(A5,D7) ; GUARDO EL NÚMERO DE COLUMNAS
        
        JSR NUEVALINEA
        
        RTS
        
IMATRIZ
        *IMPRIMO EL TÍTULO*
        
        JSR NUEVALINEA
        
        MOVE.L #IFIL,A1
        JSR IMPTXT ; IMPRIMO '*FILA: '
        
        MOVE.L D6,ONUM
        JSR IMPNUM ; IMPRIMO NÚMERO DE LA FILA
        
        JSR ASTERISCO
        JSR NUEVALINEA
        
VALORES 
        *ESTE BLOQUE RECOGE LOS VALORES INTRODUCIDOS POR TECLADO*
        
        JSR PEDIRFRACCIONARIO
        MOVE.L INUM,D3
        MOVE.L ISIGN,D2
                
        MOVE.L D7,CMAT
        MOVE.L D6,CFIL
        MOVE.L D5,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR A LEER
        MOVE.L CVAL,D4
        
        MOVE.L D3,(A4,D4) ; GUARDO EL VALOR INTRODUCIDO
        MOVE.L D2,(A3,D4)
                
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA FILA*
        
        ADD.L #1,D5 ; SIGUIENTE COLUMNA
        CMP.B (A5,D7),D5
        BEQ FINFILA
        JMP VALORES ; LE PIDO EL SIGUIENTE VALOR DE LA FILA
        
FINFILA   
        
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA MATRIZ*
        
        CLR.L D5 ; RESETEO EL CONTADOR DE COLUMNAS
        ADD.L #1,D6 ; SUMO 1 AL CONTADOR DE FILAS
        CMP.B (A6,D7),D6
        BNE IMATRIZ
        CLR.L  D6 ; RESETEO EL CONTADOR DE FILAS
        ADD.L #1,D7 ; SUMO 1 AL CONTADOR DE MATRICES    
  
        RTS ; ESTE RTS VUELVE AL FINAL DEL BLOQUE LLAMADO MATRICES


*REGISTROS PARA EL BLOQUE OPERACIONES*

*D1 --> NÚMERO DE OPERACIÓN A REALIZAR
*D0 --> OPERACIONES

*A6 --> DIRECCIONES DEL NÚMERO DE FILAS
*A5 --> DIRECCIONES DEL NÚMERO DE COLUMNAS
*A4 --> DIRECCIONES DE LOS VALORES
*A1 --> TEXTO A IMPRIMIR POR LA CONSOLA 


OPERACIONES

        JSR RESET ; RESETEO REGISTROS POR SEGURIDAD
        
        *IMPRIMO EL MENU*
        
        MOVE.L #OPEA,A1
        JSR IMPTXT ; IMPRIMO TITULO

        MOVE.L #OPEB,A1
        JSR IMPTXT ; IMPRIMO OPERACION 1

        MOVE.L #OPEC,A1
        JSR IMPTXT ; IMPRIMO OPERACION 2
        
        MOVE.L #OPED,A1
        JSR IMPTXT ; IMPRIMO OPERACION 3
        
        MOVE.L #OPEE,A1
        JSR IMPTXT ; IMPRIMO OPERACION 4
        
        MOVE.L #OPEF,A1
        JSR IMPTXT ; IMPRIMO OPERACION 5
        
        MOVE.L #IOPE,A1
        JSR IMPTXT ; PIDO EL NÚMERO DE OPERACIÓN A REALIZAR
        
        JSR PEDIRNUMERO ; ENTRADA DEL NÚMERO
        
        MOVE.L INUM,D0
        MOVE.B D0,D1 ; GUARDO EL NÚMERO DE OPERACION
        
        *REALIZAR OPERACION ELEGIDA*
        
        CMP.B #1,D1
        BEQ OPVISUALIZAR
        
        CMP.B #2,D1
        BEQ OPCOPIAR
                
        CMP.B #3,D1
        BEQ OPINTRODUCIR
        
        CMP.B #4,D1
        BEQ OPMULTIPLICAR 
        
        CMP.B #5,D1
        BEQ OPTRIANGULAR

    
 
OTRAOPERACION
        
        JSR RESET
        
        JSR NUEVALINEA
        
        MOVE.L #OTROPET,A1
        JSR IMPTXT ; IMPRIMO OTRA OPERACIÓN
        
        MOVE.L #2,D0
        MOVE.L #OTROPE,A1
        TRAP #15
        
        MOVE.B OTROPE,D1
        
        CMP.B #83,D1
        BEQ OPERACIONES
        
        CMP.B #115,D1
        BEQ OPERACIONES
        
        RTS
               
                
*REGISTROS PARA EL BLOQUE VISUALIZAR*

*D7 --> NUMERO DE LA MATRIZ
*D6 --> CONTADOR DE FILAS
*D5 --> CONTADOR DE COLUMNAS
*D4 --> POSICION DEL VALOR A LEER
*D0 --> OPERACIONES

*A6 --> DIRECCIONES DEL NÚMERO DE FILAS
*A5 --> DIRECCIONES DEL NÚMERO DE COLUMNAS
*A4 --> DIRECCIONES DE LOS VALORES
*A1 --> TEXTO A IMPRIMIR POR LA CONSOLA 
        
OPVISUALIZAR

        JSR RESET ; RESETEO REGISTROS POR SEGURIDAD
        
        *IMPRIMO EL TITULO*
        
        MOVE.L #VIS,A1
        JSR IMPTXT ; IMPRIMO TITULO
        
        MOVE.L #NMAT,A1
        JSR IMPTXT ; PIDO EL NÚMERO DE MATRIZ
        
        JSR PEDIRNUMERO ; ENTRADA DEL NÚMERO
        
        SUB.L #1,INUM ; LE RESTO 1 PARA ENCONTRAR LOS VALORES EN LA TABLA
        MOVE.L INUM,D0
        MOVE.B D0,D7 ; GUARDO EL NÚMERO DE MATRIZ

VISRESULTADO

        *INDICAR MATRIZ*
        
        JSR NUEVALINEA
        
        MOVE.L #MAT,A1
        JSR IMPTXT ; IMPRIMO '*MATRIZ: '
        
        MOVE.L D7,ONUM
        JSR IMPNUM ; IMPRIMO NÚMERO DE LA MATRIZ
        
        JSR ASTERISCO
        JSR SALTOLINEA      
        JSR NUEVALINEA
        
VISUALIZAR

        MOVE.L D7,CMAT
        MOVE.L D6,CFIL
        MOVE.L D5,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR A LEER
        MOVE.L CVAL,D4    
        
        MOVE.L (A4,D4),OVAL
        MOVE.L (A3,D4),OSIGN
        JSR IMPVAL
        
        JSR TABULADOR

        
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA FILA*
        
        ADD.L #1,D5 ; SIGUIENTE COLUMNA
        CMP.B (A5,D7),D5
        BEQ FINALFILAVIS
        JMP VISUALIZAR ; SIGUIENTE FILA
        
FINALFILAVIS   
        
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA MATRIZ*
                
        JSR NUEVALINEA
        
        CLR.L D5 ; RESETEO EL CONTADOR DE COLUMNAS
        ADD.L #1,D6 ; SUMO 1 AL CONTADOR DE FILAS
        CMP.B (A6,D7),D6
        BNE VISUALIZAR
  
        JMP OTRAOPERACION ; SE SOLICITA SI SE DESEA REALIZAR OTRA OPERACIÓN
        
*REGISTROS PARA EL BLOQUE COPIAR*

*D7 --> NUMERO DE LA MATRIZ ORIGINAL
*D6 --> CONTADOR DE FILAS
*D5 --> CONTADOR DE COLUMNAS
*D4 --> POSICION DEL VALOR A COPIAR
*D3 --> VALOR A COPIAR
*D2 --> NUMERO DE LA MATRIZ COPIA
*D1 --> SIGNO DEL VALOR A COPIAR / VARIOS
*D0 --> VARIOS

*A6 --> DIRECCIONES DEL NÚMERO DE FILAS
*A5 --> DIRECCIONES DEL NÚMERO DE COLUMNAS
*A4 --> DIRECCIONES DE LOS VALORES
*A1 --> TEXTO A IMPRIMIR POR LA CONSOLA         
             
OPCOPIAR
        
        JSR RESET ; RESETEO REGISTROS POR SEGURIDAD
        
        *IMPRIMO EL TITULO*
        
        MOVE.L #COP,A1
        JSR IMPTXT ; IMPRIMO TITULO
        
        MOVE.L #NMATO,A1
        JSR IMPTXT ; PIDO EL NÚMERO DE MATRIZ A COPIAR
        
        JSR PEDIRNUMERO ; ENTRADA DEL NÚMERO
        
        SUB.L #1,INUM ; LE RESTO 1 PARA ENCONTRAR LOS VALORES EN LA TABLA
        MOVE.L INUM,D0
        MOVE.B D0,D7 ; GUARDO EL NÚMERO DE MATRIZ
        
        JSR NUEVALINEA
        
        MOVE.L #NMATC,A1
        JSR IMPTXT ; PIDO EL NÚMERO DE MATRIZ DONDE COPIAR
        
        JSR PEDIRNUMERO ; ENTRADA DEL NÚMERO
        
        SUB.L #1,INUM ; LE RESTO 1 PARA ENCONTRAR LOS VALORES EN LA TABLA
        MOVE.L INUM,D0
        MOVE.B D0,D2 ; GUARDO EL NÚMERO DE MATRIZ
        
        MOVE.B (A6,D7),D0
        MOVE.B (A5,D7),D1
        MOVE.B D0,(A6,D2)
        MOVE.B D1,(A5,D2) ; GUARDO LAS DIMENSIONES DE LA MATRIZ COPIA
                
COPIAR
        
        MOVE.L D7,CMAT
        MOVE.L D6,CFIL
        MOVE.L D5,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR A COPIAR
        MOVE.L CVAL,D4 
        
        MOVE.L (A4,D4),D3 ; OBTENGO EL VALOR A COPIAR
        MOVE.L (A3,D4),D1
        
        MOVE.L D2,CMAT
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR DONDE COPIAR
        MOVE.L CVAL,D4 

        MOVE.L D3,(A4,D4) ; COPIO EL VALOR EN SU POSICION
        MOVE.L D1,(A3,D4)
        
 *COMPRUEBO SI HE LLEGADO AL FINAL DE LA FILA*
        
        ADD.L #1,D5 ; SIGUIENTE COLUMNA
        CMP.B (A5,D7),D5
        BEQ FINALFILACOP
        JMP COPIAR ; SIGUIENTE FILA
        
FINALFILACOP  
        
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA MATRIZ*
                
        JSR NUEVALINEA
        
        CLR.L D5 ; RESETEO EL CONTADOR DE COLUMNAS
        ADD.L #1,D6 ; SUMO 1 AL CONTADOR DE FILAS
        CMP.B (A6,D7),D6
        BNE COPIAR
  
        MOVE.L D2,OVAL ; GUARDO EL NÚMERO DE LA MATRIZ A VISUALIZAR
        JSR RESET ; RESETEO CONTADORES PARA NO INTERFERIR CON LA VISUALIZACIÓN
        MOVE.L OVAL,D7
        JMP VISRESULTADO ; MUESTRO LA MATRIZ COPIA      
        
*REGISTROS PARA EL BLOQUE INTRODUCIR MATRICES*

*D7 --> NÚMERO DE MATRIZ
*D0 --> OPERACIONES

*A6 --> DIRECCIONES DEL NÚMERO DE FILAS
*A5 --> DIRECCIONES DEL NÚMERO DE COLUMNAS
*A4 --> DIRECCIONES DE LOS VALORES
*A1 --> TEXTO A IMPRIMIR POR LA CONSOLA        
        
OPINTRODUCIR

        JSR RESET ; RESETEO REGISTROS POR SEGURIDAD
        
        *IMPRIMO EL TITULO*
        
        MOVE.L #INT,A1
        JSR IMPTXT ; IMPRIMO TITULO
        
        MOVE.L #NMAT,A1
        JSR IMPTXT ; PIDO EL NÚMERO DE MATRIZ
        
        JSR PEDIRNUMERO ; ENTRADA DEL NÚMERO
        
        SUB.L #1,INUM ; LE RESTO 1 PARA ENCONTRAR LOS VALORES EN LA TABLA
        MOVE.L INUM,D0
        MOVE.B D0,D7 ; GUARDO EL NÚMERO DE MATRIZ   
        
        MOVE.L D7,OVAL ; GUARDO EL NÚMERO DE LA MATRIZ PARA VISUALIZAR POSTERIORMENTE
        
        JSR NUEVALINEA ;SALTO DE LINEA Y PRINCIPIO DE LINEA       
        
        *PIDO NÚMERO DE FILAS Y DE COLUMNAS DE LA MATRIZ*
        
        JSR TAMANO
        
        *PIDO QUE INTRODUZCA LOS VALORES*
        
        MOVE.L #VAL,A1
        JSR IMPTXT  ; IMPRIMO LA INSTRUCCION
        
        JSR IMATRIZ ; PIDO LOS VALORES DE LA MATRIZ
        
        JSR RESET ; RESETEO CONTADORES PARA NO INTERFERIR CON LA VISUALIZACIÓN
        MOVE.L OVAL,D7
        JMP VISRESULTADO ; MUESTRO LA MATRIZ COPIA  


*REGISTROS PARA EL BLOQUE MULTIPLICAR*

*D7 --> NUMERO DE MATRIZ 1
*D6 --> NÚMERO DE MATRIZ 2
*D5 --> CONTADOR DE FILAS PRIMERA MATRIZ Y DE LA MATRIZ RESULTADO
*D4 --> CONTADOR DE COLUMNAS SEGUNDA MATRIZ Y DE LA MATRIZ RESULTADO
*D3 --> CONTADOR DE COLUMNAS PRIMERA Y FILAS SEGUNDA
*D2 --> POSICION DEL LOS VALORES DE LECTURA
*D1 --> RESULTADO DE MULTIPLICAR DOS ELEMENTOS

*A6 --> DIRECCIONES DEL NÚMERO DE FILAS
*A5 --> DIRECCIONES DEL NÚMERO DE COLUMNAS
*A4 --> DIRECCIONES DE LOS VALORES
*A1 --> TEXTO A IMPRIMIR POR LA CONSOLA 
        
OPMULTIPLICAR
        
        JSR RESET ; RESETEO LOS CONTADORES
        
        *IMPRIMO TITULO*
        
        MOVE.L #MULT,A1
        JSR IMPTXT ; IMPRIMO TITULO
        
        MOVE.L #NMATA,A1
        JSR IMPTXT ; PIDO EL NÚMERO DE LA PRIMERA MATRIZ
        
        JSR PEDIRNUMERO ; ENTRADA DEL NÚMERO
        
        SUB.L #1,INUM ; LE RESTO 1 PARA ENCONTRAR LOS VALORES EN LA TABLA
        MOVE.L INUM,D0
        MOVE.B D0,D7 ; GUARDO EL NÚMERO DE MATRIZ
        
        JSR NUEVALINEA
        
        MOVE.L #NMATB,A1
        JSR IMPTXT ; PIDO EL NÚMERO DE LA SEGUNDA MATRIZ
        
        JSR PEDIRNUMERO ; ENTRADA DEL NÚMERO
        
        SUB.L #1,INUM ; LE RESTO 1 PARA ENCONTRAR LOS VALORES EN LA TABLA
        MOVE.L INUM,D0
        MOVE.B D0,D6 ; GUARDO EL NÚMERO DE MATRIZ
        
        CLR.L D0 ; RESETEO EL CONTADOR USADO PARA GUARDAR LAS DIMENSIONES DE LA MATRIZ RESULTADO
        
        JSR NUEVALINEA
        
        JSR RESETRES ; RESETEO LOS VALORES DE LA MATRIZ RESULTADO ANTES DE REALIZAR LA OPERACIÓN
        
        *GUARDO LA DIMENSION DE LA MATRIZ RESULTADO*

        MOVE.L #8,D1        
        MOVE.B (A6,D7),D0
        MOVE.B D0,(A6,D1) ; EL NÚMERO DE FILAS SE CORRESPONDE CON EL DE LA PRIMERA MATRIZ
        MOVE.B (A5,D6),D0
        MOVE.B D0,(A5,D1) ; EL NÚMERO DE COLUMNAS SE CORRESPONDE CON EL DE LA SEGUNDA MATRIZ
        CLR.L D0 
        CLR.L D1 ; RESETEO EL CONTADOR PARA USARLO LEYENDO VALORES
        
MULTIPLICAR
               
        *COMIENZA LA MULTIPLICACIÓN*
        
        MOVE.L D7,CMAT
        MOVE.L D5,CFIL
        MOVE.L D3,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR A LEER EN LA PRIMERA MATRIZ
        MOVE.L CVAL,D2
        
        MOVE.L (A4,D2),MULOPEA ; GUARDO EL VALOR 
        MOVE.L (A3,D2),SIGNOA
        
        MOVE.L D6,CMAT
        MOVE.L D3,CFIL
        MOVE.L D4,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR A LEER EN LA SEGUNDA MATRIZ
        MOVE.L CVAL,D2
        
        MOVE.L (A4,D2),MULOPEB ; GUARDO EL VALOR
        MOVE.L (A3,D2),SIGNOB

        JSR MULTDECIMAL
        MOVE.L RMULDIV,D1 ; AHORA TENGO EN D0 EL RESULTADO DE LA MULTIPLICACIÓN
        MOVE.L RSIGNO,D0
        
        MOVE.L D1,SUMOPEA ; LO GUARDO PARA SUMARLO
        MOVE.L D0,SIGNOA 
        
        MOVE.L #8,CMAT ; GUARDO EL RESULTADO EN LA ÚLTIMA MATRIZ
        MOVE.L D5,CFIL
        MOVE.L D4,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR A ESCRIBIR
        MOVE.L CVAL,D2
        
        MOVE.L (A4,D2),D1
        MOVE.L (A3,D2),D0
        MOVE.L D1,SUMOPEB ; LO GUARDO PARA SUMARLO
        MOVE.L D0,SIGNOB

        JSR SUMASIGN

        MOVE.L RSUMA,D1
        MOVE.L RSIGNO,D0

        MOVE.L D1,(A4,D2) ;GUARDO EL VALOR YA SUMADO
        MOVE.L D0,(A3,D2) 
        
        ADD.L #1,D3
        CMP.B (A5,D7),D3 ; COMPARO SI HE LLEGADO AL FINAL DE LA FILA/COLUMNA
        BEQ FINALCOLMULT 
        JMP MULTIPLICAR
        
FINALCOLMULT        
        
        *COMPRUEBO SI HE MULTIPLICADO LA FILA POR TODAS LAS COLUMNAS*
                        
        CLR.L D3 ; RESETEO EL CONTADOR DE COLUMNAS/FILAS
        ADD.L #1,D4 ; SUMO 1 AL CONTADOR DE COLUMNAS DE LA SEGUNDA MATRIZ
        CMP.B (A5,D6),D4 
        BEQ FINALFILAMULT
        JMP MULTIPLICAR
              
FINALFILAMULT

        *COMPRUEBO SI HE MULTIPLICADO TODAS LAS FILAS POR TODAS LAS COLUMNAS*
                        
        CLR.L D3 ; RESETEO EL CONTADOR DE COLUMNAS/FILAS
        CLR.L D4 ; RESETEO EL CONTADOR DE COLUMNAS DE LA SEGUNDA MATRIZ
        ADD.L #1,D5 ; SUMO 1 AL CONTADOR DE FILAS DE LA PRIMERA MATRIZ
        CMP.B (A6,D7),D5
        BNE MULTIPLICAR
        
        JSR RESET ; RESETEO CONTADORES PARA NO INTERFERIR CON LA VISUALIZACIÓN
        MOVE.L #8,D7
        JMP VISRESULTADO

        
*REGISTROS PARA EL BLOQUE TRIANGULAR*

*D7 --> NUMERO DE LA MATRIZ
*D6 --> CONTADOR DE FILAS
*D5 --> CONTADOR DE COLUMNAS
*D4 --> POSICION DEL VALOR A LEER
*D3 --> VALOR A COPIAR EN LA MATRIZ RESULTADO
*D1 --> OPERACIONES
*D0 --> OPERACIONES

*A6 --> DIRECCIONES DEL NÚMERO DE FILAS
*A5 --> DIRECCIONES DEL NÚMERO DE COLUMNAS
*A4 --> DIRECCIONES DE LOS VALORES
*A1 --> TEXTO A IMPRIMIR POR LA CONSOLA                

OPTRIANGULAR

        JSR RESET ; RESETEO REGISTROS POR SEGURIDAD
        
        *IMPRIMO EL TITULO*
        
        MOVE.L #TRI,A1
        JSR IMPTXT ; IMPRIMO TITULO
        
        MOVE.L #NMAT,A1
        JSR IMPTXT ; PIDO EL NÚMERO DE MATRIZ
        
        JSR PEDIRNUMERO ; ENTRADA DEL NÚMERO
        
        SUB.L #1,INUM ; LE RESTO 1 PARA ENCONTRAR LOS VALORES EN LA TABLA
        MOVE.L INUM,D0
        MOVE.B D0,D7 ; GUARDO EL NÚMERO DE MATRIZ
        
        JSR RESETRES ; RESETEO LOS VALORES DE LA MATRIZ RESULTADO ANTES DE REALIZAR LA OPERACIÓN 

        *GUARDO LA DIMENSION DE LA MATRIZ RESULTADO*
        
        MOVE.B (A6,D7),D0
        MOVE.L #8,D1
        MOVE.B D0,(A6,D1)
        MOVE.B D0,(A5,D1) ; USO EL MISMO VALOR PQ LA MATRIZ RESULTADO ES CUADRADA
        CLR.L D0 
        CLR.L D1 
        
SUPERIORINFERIOR

        JSR NUEVALINEA
        
        MOVE.L #SUPINFT,A1
        JSR IMPTXT ; IMPRIMO SUPERIOR INFERIOR
        
        MOVE.L #2,D0
        MOVE.L #SUPINF,A1
        TRAP #15
        
        MOVE.B SUPINF,D1
        
        CMP.B #83,D1
        BEQ TRIANGULARSUPERIOR
        
        CMP.B #115,D1
        BEQ TRIANGULARSUPERIOR
        
        CMP.B #73,D1
        BEQ TRIANGULARINFERIOR
        
        CMP.B #105,D1
        BEQ TRIANGULARINFERIOR
        
        JMP SUPERIORINFERIOR
        
TRIANGULARSUPERIOR

        MOVE.L D7,CMAT
        MOVE.L D6,CFIL
        MOVE.L D5,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR
        MOVE.L CVAL,D4    
        
        MOVE.L (A4,D4),D3
        MOVE.L (A3,D4),D2
        
        CMP.L D6,D5
        BCS TRISUPFILA
        
        MOVE.L #8,CMAT
        MOVE.L D6,CFIL
        MOVE.L D5,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR
        MOVE.L CVAL,D4 
        
        MOVE.L D3,(A4,D4)
        MOVE.L D2,(A3,D4)

TRISUPFILA
       
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA FILA*
        
        ADD.L #1,D5 ; SIGUIENTE COLUMNA
        CMP.B (A5,D7),D5
        BEQ TRISUPFINAL
        JMP TRIANGULARSUPERIOR ; SIGUIENTE FILA
        
TRISUPFINAL  
        
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA MATRIZ*
               
        CLR.L D5 ; RESETEO EL CONTADOR DE COLUMNAS
        ADD.L #1,D6 ; SUMO 1 AL CONTADOR DE FILAS
        CMP.B (A6,D7),D6
        BNE TRIANGULARSUPERIOR
        
        JSR RESET ; RESETEO CONTADORES PARA NO INTERFERIR CON LA VISUALIZACIÓN
        MOVE.L #8,D7
        JMP VISRESULTADO
         
TRIANGULARINFERIOR

        MOVE.L D7,CMAT
        MOVE.L D6,CFIL
        MOVE.L D5,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR
        MOVE.L CVAL,D4    
        
        MOVE.L (A4,D4),D3
        MOVE.L (A3,D4),D2
        
        CMP.L D6,D5
        BHI TRIINFFILA
        
        MOVE.L #8,CMAT
        MOVE.L D6,CFIL
        MOVE.L D5,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR
        MOVE.L CVAL,D4 
        
        MOVE.L D3,(A4,D4)
        MOVE.L D2,(A3,D4)

TRIINFFILA
       
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA FILA*
        
        ADD.L #1,D5 ; SIGUIENTE COLUMNA
        CMP.B (A5,D7),D5
        BEQ TRIINFFINAL
        JMP TRIANGULARINFERIOR ; SIGUIENTE FILA
        
TRIINFFINAL  
        
        *COMPRUEBO SI HE LLEGADO AL FINAL DE LA MATRIZ*
               
        CLR.L D5 ; RESETEO EL CONTADOR DE COLUMNAS
        ADD.L #1,D6 ; SUMO 1 AL CONTADOR DE FILAS
        CMP.B (A6,D7),D6
        BNE TRIANGULARINFERIOR
        
        JSR RESET ; RESETEO CONTADORES PARA NO INTERFERIR CON LA VISUALIZACIÓN
        MOVE.L #8,D7
        JMP VISRESULTADO
       
*CADENAS DE TEXTO (EL 10 SIGNIFICA SALTO DE LINEA, EL 13 ES UNA NUEVA PÁGINA Y EL 13 ES PARA IR AL PRINCIPIO DE LA LINEA COMO EN LA MÁQUINA DE ESCRIBIR)*

INI     DC.B '* BIENVENIDO A LA CALCULADORA DE MATRICES *',10,10,10,13,0
INSTRA  DC.B 'INSTRUCCIONES DE USO PARA CORRECTO FUNCIONAMIENTO:',10,10,13,'1.- MÁXIMO 8 MATRICES, LA NÚMERO 9 ALMACENA LOS RESULTADOS DE LAS OPERACIONES',10,13,'MULTIPLICAR Y TRIANGULAR.',10,10,13,0
INSTRB  DC.B '2.- EL TAMAÑO MÁXIMO DE LAS MATRICES ES 9X9.',10,10,13,'3.- SI DESEAS UTILIZAR EL RESULTADO DE UNA OPERACIÓN EN OTRA DISTINTA DEBES',10,13,'COPIARLO PRIMERO.',10,10,13,0
INSTRC  DC.B '4.- PUEDES UTILIZAR TANTO LA COMA COMO EL PUNTO PARA INTRODUCIR DECIMALES.',10,10,13,0
INSTRD  DC.B '5.- PARA MULTIPLICAR MATRICES EL NÚMERO DE COLUMNAS DE LA PRIMERA DEBE',10,13,'COINCIDIR CON EL NÚMERO DE FILAS DE LA SEGUNDA.',10,10,13,0
INSTRE  DC.B '6.- PARA OBTENER LA MATRIZ TRIANGULAR, LA ORIGINAL DEBE SER CUADRADA.',10,10,10,13,0
NUM     DC.B '-->INTRODUCE EL NÚMERO DE MATRICES: ',0
MAT     DC.B '*MATRIZ: ',0
FIL     DC.B '-->INTRODUCE EL NÚMERO DE FILAS: ',0
COL     DC.B '-->INTRODUCE EL NÚMERO DE COLUMNAS: ',0
VAL     DC.B '-->INTRODUCE LOS ELEMENTOS: ',10,13,0
IFIL    DC.B '*FILA: ',0
OPEA    DC.B 12,'* OPERACIONES A REALIZAR *',10,10,13,0
OPEB    DC.B '1.- VISUALIZAR MATRIZ',10,13,0
OPEC    DC.B '2.- COPIAR MATRIZ',10,13,0
OPED    DC.B '3.- INTRODUCIR MATRIZ',10,13,0
OPEE    DC.B '4.- MULTIPLICAR MATRICES',10,13,0
OPEF    DC.B '5.- MATRIZ TRIANGULAR',10,10,13,0
IOPE    DC.B '-->INTRODUCE EL NÚMERO DE OPERACIÓN: ',0
NMAT    DC.B '-->INTRODUCE EL NÚMERO DE LA MATRIZ: ',0
NMATA   DC.B '-->INTRODUCE EL NÚMERO DE LA PRIMERA MATRIZ: ',0
NMATB   DC.B '-->INTRODUCE EL NÚMERO DE LA SEGUNDA MATRIZ: ',0
NMATO   DC.B '-->INTRODUCE EL NÚMERO DE LA MATRIZ A COPIAR: ',0
NMATC   DC.B '-->INTRODUCE EL NÚMERO DE LA MATRIZ DONDE COPIARLA: ',0
VIS     DC.B 12,'* VISUALIZAR MATRIZ *',10,10,13,0
COP     DC.B 12,'* COPIAR MATRIZ *',10,10,13,0
INT     DC.B 12,'* INTRODUCIR MATRIZ *',10,10,13,0
MULT    DC.B 12,'* MULTIPLICAR MATRICES *',10,10,13,0
TRI     DC.B 12,'* MATRIZ TRIANGULAR *',10,10,13,0
SUPINFT DC.B '-->¿DESEA REALIZAR LA MATRIZ TRIANGULAR SUPERIOR O INFERIOR?(S/I): ',0
OTROPET DC.B '-->¿DESEA REALIZAR OTRA OPERACIÓN?(S/N): ',0


*VALORES PARA LAS SUBRUTINAS PRINCIPALES*

MNUM    DS.B 1
FILAS   DS.B 9
COLS    DS.B 9
OTROPE  DS.B 1
SUPINF  DS.B 1
VALS    DS.L 729
SIGN    DS.L 729 ; LOS SIGNOS SE GUARDAN EN .L PARA QUE LAS POSICIONES SE CORRESPONDAN CON LAS DE LOS VALORES DE LAS MATRICES



        
*SUBRUTINAS QUE SE REUTILIZAN Y NO FORMAN PARTE DEL PROGRAMA PRINCIPAL*
        
RESET
        
        CLR.L D0 
        CLR.L D1 
        CLR.L D2 
        CLR.L D3 
        CLR.L D4 
        CLR.L D5 
        CLR.L D6 
        CLR.L D7 
        MOVE.L #0,A0 
        MOVE.L #0,A1
        MOVE.L #0,A2
        MOVE.L #0,A3
        MOVE.L #0,A4
        MOVE.L #0,A5
        MOVE.L #0,A6
             
        MOVE.L #FILAS,A6 ; PUNTERO PARA LA TABLA DE DIRECCIONES DEL NÚMERO DE FILAS
        MOVE.L #COLS,A5 ; PUNTERO PARA LA TABLA DE DIRECCIONES DEL NÚMERO DE COLUMNAS
        MOVE.L #VALS,A4 ; PUNTERO PARA LA TABLA DE DIRECCIONES DE LOS VALORES DE LA FILA Y LA COLUMNA DE UNA MATRIZ
        MOVE.L #SIGN,A3 ; PUNTERO PARA LA TABLA DE DIRECCIONES DE LOS SIGNOS DE LOS VALORES
        
        RTS
        
SALTOLINEA
        
        MOVEM.L D0-D7/A0-A6,-(A7) ; GUARDO EL VALOR DE LOS REGISTROS AL ENTRAR A LA SUBRUTINA
        MOVE.L #6,D0
        MOVE.L #10,D1
        TRAP #15
        MOVEM.L (A7)+,D0-D7/A0-A6 ; RECUPERO EL VALOR DE LOS REGISTROS AL SALIR
        
        RTS
      
NUEVALINEA
        
        MOVEM.L D0-D7/A0-A6,-(A7)
        JSR SALTOLINEA
        MOVE.L #6,D0
        MOVE.L #13,D1
        TRAP #15
        MOVEM.L (A7)+,D0-D7/A0-A6
        
        RTS     

NUEVAPAGINA
        
        MOVEM.L D0-D7/A0-A6,-(A7)        
        MOVE.L #6,D0
        MOVE.L #12,D1
        TRAP #15
        MOVEM.L (A7)+,D0-D7/A0-A6
        
        RTS 

ASTERISCO
        
        MOVEM.L D0-D7/A0-A6,-(A7)        
        MOVE.L #6,D0
        MOVE.L #42,D1
        TRAP #15
        MOVEM.L (A7)+,D0-D7/A0-A6 
       
        RTS 
        
TABULADOR
        
        MOVEM.L D0-D7/A0-A6,-(A7) 
        MOVE.L #6,D0
        MOVE.L #9,D1
        TRAP #15
        MOVEM.L (A7)+,D0-D7/A0-A6 
       
        RTS

APOSTROFE

        MOVEM.L D0-D7/A0-A6,-(A7) 
        MOVE.L #6,D0
        MOVE.L #48,D1
        TRAP #15        
        MOVE.L #6,D0
        MOVE.L #39,D1
        TRAP #15
        MOVEM.L (A7)+,D0-D7/A0-A6 
       
        RTS
        
IMPTXT
        
        MOVEM.L D0-D7/A2-A6,-(A7)
        MOVE.L #14,D0
        TRAP #15        
        MOVEM.L (A7)+,D0-D7/A2-A6 

        RTS


IMPNUM
        MOVEM.L D0-D7/A0-A6,-(A7)
        ADD.L #1,ONUM ; SUMO 1 PQ LOS CONTADORES EMPIEZAN A CONTAR POR 0 Y SUMO 48 PARA CONVERTIR A ASCII
        MOVE.L #3,D0
        MOVE.L ONUM,D1
        TRAP #15
        
        MOVEM.L (A7)+,D0-D7/A0-A6

        RTS
        
IMPVAL
        MOVEM.L D0-D7/A0-A6,-(A7)
        JSR RESET
        
        MOVE.L OVAL,D3
        MOVE.L OSIGN,D5
        
        CMP.L #1,D5
        BNE IMPENTERO
        
        CMP.L #0,D3
        BEQ IMPENTERO
        
        MOVE.L #6,D0
        MOVE.L #45,D1
        TRAP #15 ; IMPRIMO SIGNO NEGATIVO
               
IMPENTERO   
     
        AND.L #$FFFF0000,D3 ; ELIMINO LA PARTE FRACCIONAL
        
        SWAP D3
        MOVE.L #3,D0
        MOVE.L D3,D1
        TRAP #15 ; IMPRIMO PARTE ENTERA
        
        MOVE.L #6,D0
        MOVE.L #39,D1
        TRAP #15 ; IMPRIMO APOSTROFE
        
        MOVE.L OVAL,D3
        AND.L #$0000FFFF,D3 ; ELIMINO LA PARTE ENTERA
        
        CLR.L D1
        
CORRERCOMA

        MULU #10,D3
        SWAP D3 ; D.E
        MOVE.W D3,D2 ; GUARDO EL DECIMAL
        MULU #10,D1
        ADD.L D2,D1 ; SUMO EL DECIMAL
        SWAP D3 ; E.D
        AND.L #$0000FFFF,D3 ; ELIMINO EL DECIMAL
        
        CMP.W #0,D3 ; COMPRUEBO SI QUEDAN DECIMALES 
        BEQ IMPDECIMAL
        
        ADD.L #1,D4
        CMP.W #4,D4 ; COMPRUEB SI HE SACADO MAS DE 4 DECIMAL
        BNE CORRERCOMA

REDONDEAR
        
        MULU #10,D3
        SWAP D3 ; D.E
        MOVE.W D3,D2 ; GUARDO EL SIGUIENTE DECIMAL
        
        CMP.B #5,D2
        BCS IMPDECIMAL ; SI ES MENOR QUE 5 NO HACE FALTA SUMAR 1
        
        ADD.L #1,D1
        
IMPDECIMAL
        
        MOVE.L #3,D0 
        TRAP #15 ; IMPRIMO LOS DECIMALES
        
        MOVEM.L (A7)+,D0-D7/A0-A6
        RTS
        
PEDIRNUMERO

        MOVEM.L D0-D7/A0-A6,-(A7)
        MOVE.L #4,D0 
        TRAP #15
        MOVE.L D1, INUM
        MOVEM.L (A7)+,D0-D7/A0-A6 
        
        RTS
        
*REGISTROS PARA EL BLOQUE PEDIR FRACCIONARIO*

*D7 --> ALMACENA LA PARTE DECIMAL
*D6 --> CARÁCTERES Y NÚMEROS DE LA PARTE DECIMAL
*D5 --> CONTADOR DE CARACTERES DE LA PARTE DECIMAL
*D4 --> ALMACENA LA PARTE ENTERA DEL NÚMERO 16.16
*D3 --> CARÁCTERES Y NÚMEROS DE LA PARTE ENTERA
*D2 --> CONTADOR DE CARÁCTERES DE LA PARTE ENTERA
*D1 --> VARIOS
*D0 --> VARIOS
        
        
PEDIRFRACCIONARIO

        MOVEM.L D0-D7/A0-A6,-(A7)
        JSR RESET
        
        MOVE.L #2,D0
        MOVE.L #STRDEC,A1 ; ENTRADA DE LOS NÚMEROS COMO CARACTERES ASCII (ESTO ES NECESARIO PARA DISTINGUIR CUANDO LOS DECIMALES COMIENZAN POR UN 0 Y ADEMÁS ASEGURO QUE CADA DÍGITO OCUPA UN BYTE)
        TRAP #15
        CLR.L D1
        
        MOVE.L #0,ISIGN ; GUARDO UN SIGNO POSITIVO
        
SAVEENTERO       
 
        MULU.W #10,D4 ; MULTIPLICO POR 10 PARA CORRER UN DIGITO
        MOVE.B (A1,D2),D3
        
        CMP.B #45,D3
        BEQ NEGATIVO
        
        SUB.L #48,D3 ; CONVIERTO EL CARACTER ASCII A UN NÚMERO
        ADD.L D3,D4 ; GUARDO EL NÚMERO
        ADD.L #1,D2
        MOVE.B (A1,D2),D3 ; OBTENGO EL SIGUIENTE CARÁCTER
        
        CMP.L #0,D3
        BEQ SAVENUMERO ; COMPRUEBO EL CASO DE QUE NO SE INTRODUZCAN DECIMALES
        
        CMP.L #44,D3
        BEQ PASARCOMA ; NO PUEDO USAR BNE YA QUE EN EL CASO DE QUE FUESE 46 NO SE COMPROBARIA
        
        CMP.L #46,D3
        BNE SAVEENTERO
                      
PASARCOMA
        
        ADD.L #1,D2 ; SE SALTA LA COMA
        ADD.L D2,D5 ; SE SUMA EL NÚMERO DE CARÁCTERES DE LA PARTE ENTERA Y LA COMA AL CONTADOR DE LA PARTE DECIMAL
       
SAVEDECIMAL
 
        MULU.W #10,D7
        MOVE.B (A1,D5),D6
        SUB.L #48,D6 ; CONVIERTO EL CARACTER ASCII A UN NÚMERO
        ADD.L D6,D7 ; SUMO EL VALOR DEL CARACTER
        ADD.L #1,D5 ; AÑADO 1 AL CONTADOR DE DECIMALES
        
        CLR.L D1
        CMP.B (A1,D5),D1 ; COMPRUEBO SI HE LLEGADO AL FINAL DE LA CADENA
        BNE SAVEDECIMAL
                
        SUB.L D2,D5
        CLR.L D2
        ADD.L #1,D2

MULTIPLODIEZ

        CMP.L #0,D5 ; COMPARO PARA VER SI HE LLEGADO AL FINAL
        BEQ CORRERDECIMAL
        MULU.W #10,D2
        SUB.L #1,D5
        JMP MULTIPLODIEZ      
        
CORRERDECIMAL

        SWAP D7 
        DIVU D2,D7 ; RESULTADO RESTO-COCIENTE EN D7
        AND.L #$0000FFFF,D7 ; ELIMINO EL RESTO DE LA DIVISIÓN

SAVENUMERO       
 
        SWAP D4
        MOVE.W D7,D4
        MOVE.L D4,INUM ; GUARDO EL VALOR DEL NÚMERO FRACCIONARIO
        
        MOVEM.L (A7)+,D0-D7/A0-A6
        
        RTS
        
NEGATIVO

        MOVE.L #1,ISIGN ; GUARDO UN SIGNO NEGATIVO
        ADD.L #1,D2 ; SALTO AL SIGUIENTE CARACTER
        
        JMP SAVEENTERO
        
*REGISTROS PARA EL BLOQUE CONTADOR VALOR*

*D0 --> PUNTERO QUE SEÑALA EL ELEMENTO    
    
CONTADORVALOR

        MOVEM.L D0-D7/A0-A6,-(A7)
        
        MOVE.L CMAT,D0 ; INTRODUZCO EL VALOR DEL CONTADOR DE MATRICES EN D2
        
        MULU.W #81,D0 ; COMO CADA MATRIZ TENDRÁ MÁXIMO 81 VALORES
        MULU.W #4,D0 ; COMO CADA VALOR OCUPA 4 BYTES
        MOVE.L D0,CVAL ; INTRODUZCO EL CONTADOR DEL PRIMER ELEMENTO DE LA MATRIZ
        
        MOVE.L CFIL,D0 ; INTRODUZCO EL VALOR DEL CONTADOR DE FILAS EN D2
        
        MULU.W #9,D0 ; COMO CADA FILA TENDRÁ COMO MÁXIMO 9 VALORES
        MULU.W #4,D0 ; COMO CADA VALOR OCUPA 4 BYTES
        ADD.L D0,CVAL ; AHORA EL CONTADOR ESTA SEÑALANDO AL PRIMER ELEMENTO DE LA FILA
        
        MOVE.L CCOL,D0
        MULU.W #4,D0 ; CADA COLUMNA OCUPA 4 BYTES
        ADD.L D0,CVAL ; AHORA EL PUNTERO ESTÁ SEÑALANDO AL ELEMENTO DESEADO   
        
        MOVEM.L (A7)+,D0-D7/A0-A6      
        
        RTS

*REGISTROS PARA EL BLOQUE SUMA CON SIGNO*

*D0 --> SIGNO OPERANDO A
*D1 --> SIGNO OPERANDO B
*D2 --> OPERANDO A
*D3 --> OPERANDO B

SUMASIGN

        MOVEM.L D0-D7/A0-A6,-(A7)
        
        JSR RESET
        
        MOVE.L SIGNOA,D0
        MOVE.L SIGNOB,D1
        MOVE.L SUMOPEA,D2
        MOVE.L SUMOPEB,D3
        
        CMP.B D0,D1
        BNE RESTASIGN ; COMPRUEBO SI SE HA DE SUMAR O RESTAR COMPARANDO LOS SIGNOS DE LOS SUMANDOS
        
        ADD.L D2,D3 ; SUMA DE NÚMEROS CON EL MISMO SIGNO
        MOVE.L D3,RSUMA ; ALMACENO EL RESULTADO
        MOVE.L D1,RSIGNO ; ALMACENO EL SIGNO
        
        MOVEM.L (A7)+,D0-D7/A0-A6              
        RTS
        
RESTASIGN
        
        CMP.L D2,D3 ; COMPRUEBO CUAL ES EL MAYOR DE LOS DOS TANTO PARA SABER EL SIGNO DEL RESULTADO COMO PARA RESTARLE EL MENOR
        BCS RESTAAB
        
        *RESTA B-A*
        SUB.L D2,D3
        MOVE.L D3,RSUMA
        MOVE.L D1,RSIGNO
        
        MOVEM.L (A7)+,D0-D7/A0-A6             
        RTS
        
RESTAAB

        *RESTA A-B*
        SUB.L D3,D2
        MOVE.L D2,RSUMA
        MOVE.L D0,RSIGNO
        
        MOVEM.L (A7)+,D0-D7/A0-A6             
        RTS

*REGISTROS PARA EL BLOQUE DIVDECIMAL*

*D4 --> COCIENTE FINAL
*D3 --> RESTO
*D2 --> DECIMAL
*D1 --> DIVISOR
*D0 --> DIVIDENDO

*EN UN PRINCIPIO SE UTILIZABA PARA CONVERTIR LOS NÚMEROS A FORMATO 16.16, POSTERIORMENTE NOS DIMOS CUENTA DE QUE NO ERA NECESARIO*
        
DIVDECIMAL

        MOVEM.L D0-D7/A0-A6,-(A7)
        
        JSR RESET
        
        MOVE.L DIVID,D0
        MOVE.L DIVIS,D1
        
        MOVE.W D0,D2 ; GUARDO LA PARTE DECIMAL
        CLR.W D0 ; ELIMINO LA PARTE DECIMAL
        SWAP D0 ; PARTE ENTERA EN LOS ÚLTIMOS 16 BITS
        
        * PARTE ENTERA ENTRE DIVISOR *
        DIVU.W D1,D0
        SWAP D0
        MOVE.W D0,D3 ; GUARDO EL RESTO
        CLR.W D0 ; ELIMINO EL RESTO
        ADD.L D0,D4 ; SUMO COCIENTE PARTE ENTERA ENTRE DIVISOR EN LOS PRIMEROS 16 BITS
        
        * PARTE FRACCIONAL ENTRE DIVISOR *        
        DIVU.W D1,D2
        AND.L #$0000FFFF,D2 ; ELIMINO EL RESTO
        ADD.L D2,D4 ; SUMO COCIENTE PARTE FRACCIONAL ENTRE DIVISOR EN LOS ULTIMOS 16 BITS
        
        * RESTO PARTE ENTERA ENTRE DIVISOR*
        SWAP D3
        DIVU.W D1,D3
        AND.L #$0000FFFF,D3 ; ELIMINO EL RESTO
        ADD.L D3,D4 ; SUMO COCIENTE RESTO ENTRE DIVISOR EN LOS ULTIMOS 16 BITS
        
        MOVE.L D4,RMULDIV
        
        JSR RESET ; RESETEO LOS REGISTROS
        
        * 
        
        MOVE.L SIGNOA,D0
        MOVE.L SIGNOB,D1
        
        CMP.L D0,D1 ; SI LOS SIGNOS SON IGUALES EL RESULTADO ES POSITIVO
        BEQ DIVPOS
        
        MOVE.L #1,RSIGNO ; SIGNO NEGATIVO
                
        MOVEM.L (A7)+,D0-D7/A0-A6   
        RTS
        
DIVPOS   
        MOVE.L #0,RSIGNO ; SIGNO POSITIVO
        
        MOVEM.L (A7)+,D0-D7/A0-A6   
        RTS
              
*REGISTROS PARA EL BLOQUE MULTDECIMAL*

*D2 --> RESULTADO FINAL
*D1 --> OPERANDO B
*D0 --> OPERANDO A
        
        
MULTDECIMAL

        MOVEM.L D0-D7/A0-A6,-(A7)
        JSR RESET
        
        *1FX2F*
        
        MOVE.L MULOPEA,D0
        MOVE.L MULOPEB,D1
        MULU.W D0,D1
        CLR.W D1 ; ELIMINO LOS ULTIMOS 16 BITS YA QUE EL RESULTADO SERA 16.16 Y NO 32.32
        SWAP D1        
        ADD.L D1,D2
        
        *1FX2E*
        MOVE.L MULOPEA,D0
        MOVE.L MULOPEB,D1
        SWAP D1
        MULU.W D0,D1
        ADD.L D1,D2
        
        *1EX2F*
        MOVE.L MULOPEA,D0
        MOVE.L MULOPEB,D1
        SWAP D0
        MULU.W D0,D1
        ADD.L D1,D2
        
        *1EX2E*
        MOVE.L MULOPEA,D0
        MOVE.L MULOPEB,D1
        SWAP D0
        SWAP D1
        MULU.W D0,D1
        SWAP D1
        CLR.W D1 ; ELIMINO LOS 16 PRIMEROS BITS
        ADD.L D1,D2
        
        MOVE.L D2,RMULDIV ; GUARDO EL RESULTADO FINAL
        
        JSR RESET ; RESETEO LOS REGISTROS
        
        MOVE.L SIGNOA,D0
        MOVE.L SIGNOB,D1
        
        CMP.L D0,D1 ; SI LOS SIGNOS SON IGUALES EL RESULTADO ES POSITIVO
        BEQ MULPOS
        
        MOVE.L #1,RSIGNO ; SIGNO NEGATIVO
                
        MOVEM.L (A7)+,D0-D7/A0-A6   
        RTS
        
MULPOS        
        MOVE.L #0,RSIGNO ; SIGNO POSITIVO
        
        MOVEM.L (A7)+,D0-D7/A0-A6   
        RTS


* ESTE RESET SE ENCARGA DE PONER A CERO TODOS LOS VALORES DE LA MATRIZ RESULTADO ANTES DE REALIZAR UNA OPERACIÓN *
* ESTO SE HACE PARA ASEGURAR QUE NO HAY SE MEZCLAN VALORES ANTERIORES A LOS RESULTADOS DE LA OPERACIÓN ACTUAL *

RESETRES

        MOVEM.L D0-D7/A0-A6,-(A7)
        
        JSR RESET
        
        MOVE.L #8,CMAT
        MOVE.L #0,CFIL
        MOVE.L #0,CCOL
        JSR CONTADORVALOR ; SUBRUTINA QUE INDICA LA POSICION DEL VALOR A LEER EN LA PRIMERA MATRIZ
        MOVE.L CVAL,D0
        
RESETVAL

        CLR.L (A4,D0) ; RESETEO EL VALOR
        CLR.L (A3,D0) ; RESETEO EL SIGNO
        ADD.L #4,D0
        ADD.L #1,D1
        CMP.B #81,D1
        BNE RESETVAL
        
        MOVEM.L (A7)+,D0-D7/A0-A6 
        RTS
        
        
*VALORES PARA LAS SUBRUTINAS*
       
INUM    DS.L 1
ISIGN   DS.L 1
ONUM    DS.L 1
OVAL    DS.L 1
OSIGN   DS.L 1
CMAT    DS.L 1
CFIL    DS.L 1
CCOL    DS.L 1
CVAL    DS.L 1
STRDEC  DS.B 10
DECIMAL DS.L 1
ENTERO  DS.L 1
SIGNOA  DS.L 1
SIGNOB  DS.L 1
SUMOPEA DS.L 1
SUMOPEB DS.L 1
MULOPEA DS.L 1 ; PRIMER OPERANDO 16.16
MULOPEB DS.L 1 ; SEGUNDO OPERANDO 16.16
DIVID   DS.L 1 ; DIVIDENDO DE LA DIVISIÓN
DIVIS   DS.L 1 ; DIVISOR DE LA DIVISIÓN
RMULDIV DS.L 1 ; RESULTADO DE LA MULTIPLICACION O DIVISIÓN EN 16.16
RSUMA   DS.L 1 ; RESULTADO DE LA SUMA
RSIGNO  DS.L 1 ; SIGNO DE LA OPERACIÓN
        
        END    START       





























*~Font name~Courier New~
*~Font size~10~
*~Tab type~1~
*~Tab size~4~
